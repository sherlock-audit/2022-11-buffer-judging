kaliberpoziomka

high

# Keeper can resolve trade with prices from other trades

---
name: Audit item
about: These are the audit items that end up in the report
title: Keeper can resolve trade with prices from other trades
labels: High
assignees: kaliberpoziomka
---

## Summary

Context: `BufferRouter.sol`
A malicious keeper can craft `prams` argument (to the function `BufferRouter.sol::resolveQueuedTrades(...)`) in such a way that the **trade** may be resolved with the price of the other **trade** initialized in the same block.

## Vulnerability Detail

Provided argument `params` is a list of objects `OpenTradeParams`. The `publisher` role provides those objects, signing them before. The signed data from `OpenTradeParams` object is: `timestamp`, `asset`, and `price`. However, the `OpenTradeParams.queueId` is not included in the signed message.  This allows the malicious keeper to provide the `OpenTradeParams` object with `queueId` not corresponding to the rest of the data (`timestamp`, `asset`, and `price`), since it is not included in signed data. 
After the signature verification the function `resolveQueuedTrades(...)` checks if the provided timestamp and the timestamp of trade stored in `queuedTrades` array under the `queueId` match. Since creation time must much, malicious keeper may only provide wrong price from another trade that was created in the same block.
At the end the function `_openQueuedTrade(...)` is called, with provided `queueId` and price.
Note that the incorrect price must fit in the slippage range checked later.

## Impact

A malicious keeper can resolve trades with prices that were not provided by the user, which may lead to executing trade not expected by the trade maker.

## Code Snippet

https://github.com/bufferfinance/Buffer-Protocol-v2/blob/83d85d9b18f1a4d09c728adaa0dde4c37406dfed/contracts/core/BufferRouter.sol#L136-L185
```solidity
function resolveQueuedTrades(OpenTradeParams[] calldata params) external {
      _validateKeeper();
      for (uint32 index = 0; index < params.length; index++) {
          OpenTradeParams memory currentParams = params[index];
          QueuedTrade memory queuedTrade = queuedTrades[
              currentParams.queueId
          ];
          bool isSignerVerifed = _validateSigner(
              currentParams.timestamp,
              currentParams.asset,
              currentParams.price,
              currentParams.signature
          );
          // Silently fail if the signature doesn't match
          if (!isSignerVerifed) {
              emit FailResolve(
                  currentParams.queueId,
                  "Router: Signature didn't match"
              );
              continue;
          }
          if (
              !queuedTrade.isQueued ||
              currentParams.timestamp != queuedTrade.queuedTime
          ) {
              // Trade has already been opened or cancelled or the timestamp is wrong.
              // So ignore this trade.
              continue;
          }

          // If the opening time is much greater than the queue time then cancel the trade
          if (block.timestamp - queuedTrade.queuedTime <= MAX_WAIT_TIME) {
              _openQueuedTrade(currentParams.queueId, currentParams.price);
          } else {
              _cancelQueuedTrade(currentParams.queueId);
              emit CancelTrade(
                  queuedTrade.user,
                  currentParams.queueId,
                  "Wait time too high"
              );
          }

          // Track the next queueIndex to be processed for user
          userNextQueueIndexToProcess[queuedTrade.user] =
              queuedTrade.userQueueIndex +
              1;
      }
      // Track the next queueIndex to be processed overall
      nextQueueIdToProcess = params[params.length - 1].queueId + 1;
  }
```

## Tool used

Manual Review

## Recommendation

Consider including the `queueId` into the message digest signed by the publisher.