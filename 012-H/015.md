KingNFT

high

# Price manipulation attack on 'resolveQueuedTrades()' of BufferRouter.sol

## Summary
The 'resolveQueuedTrades()' function miss check for
```solidity
queuedTrade.targetContract == currentParams.asset
```
A suspicious keeper can exploit it to manipulate strike price and always win the option.
https://github.com/sherlock-audit/2022-11-buffer/blob/main/contracts/contracts/core/BufferRouter.sol#L136
## Vulnerability Detail
Let's say there are two option contracts
```solidity
Obtc = BufferBinaryOptions(BTC-USD pair)
Oeth = BufferBinaryOptions(ETH-USD pair)
```
And the market prices are
```solidity
Pbtc = 16000 USD
Peth = 1200 USD
```
We can initiate a trade like this
```solidity
QueuedTrade (
    // ...
    queueId = 1000;
    timestamp = 1234;
   
   // key parameters
    isAbove = false;
    targetContract = Oeth ;
    expectedStrike = 16000 USD;
    // ..
)
```
And call 'resolveQueuedTrades()' with
```solidity
OpenTradeParams (
    queueId = 1000;
    timestamp = 1234;
    asset = Obtc;
    price = 16000 USD;
    signature = 0x****;
)
```
As the current implementation only check 'price' but miss 'asset', the order can be successfully opened.

At last, 'unlockOptions()' with correct ETH price 1200 USD, attacker can almost 100% win the option.
## Impact
Attacker can exploit the vulnerability to drain out funds from option pool.

## Code Snippet
```solidity
function resolveQueuedTrades(OpenTradeParams[] calldata params) external {
    _validateKeeper();
    for (uint32 index = 0; index < params.length; index++) {
        OpenTradeParams memory currentParams = params[index];
        QueuedTrade memory queuedTrade = queuedTrades[
            currentParams.queueId
        ];
        bool isSignerVerifed = _validateSigner(
            currentParams.timestamp,
            currentParams.asset,
            currentParams.price,
            currentParams.signature
        );
        // Silently fail if the signature doesn't match
        if (!isSignerVerifed) {
            emit FailResolve(
                currentParams.queueId,
                "Router: Signature didn't match"
            );
            continue;
        }
        if (
            !queuedTrade.isQueued ||
            currentParams.timestamp != queuedTrade.queuedTime
        ) {
            // Trade has already been opened or cancelled or the timestamp is wrong.
            // So ignore this trade.
            continue;
        }

        // If the opening time is much greater than the queue time then cancel the trade
        if (block.timestamp - queuedTrade.queuedTime <= MAX_WAIT_TIME) {
            _openQueuedTrade(currentParams.queueId, currentParams.price);
        } else {
            _cancelQueuedTrade(currentParams.queueId);
            emit CancelTrade(
                queuedTrade.user,
                currentParams.queueId,
                "Wait time too high"
            );
        }

        // Track the next queueIndex to be processed for user
        userNextQueueIndexToProcess[queuedTrade.user] =
            queuedTrade.userQueueIndex +
            1; // @audit ???
    }
    // Track the next queueIndex to be processed overall
    nextQueueIdToProcess = params[params.length - 1].queueId + 1;
}

```
## Tool used

Manual Review

## Recommendation
```solidity
function resolveQueuedTrades(OpenTradeParams[] calldata params) external {
    _validateKeeper();
    for (uint32 index = 0; index < params.length; index++) {
        OpenTradeParams memory currentParams = params[index];
        QueuedTrade memory queuedTrade = queuedTrades[
            currentParams.queueId
        ];
        // ...

        if (queuedTrade.targetContract != currentParams.asset) {
            continue;
        }
        
        // ...
    }
    // ...
}
```
